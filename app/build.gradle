apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {

    signingConfigs {
        release
    }

    buildTypes {
        release {
            debuggable false
            jniDebuggable false
            minifyEnabled false
            shrinkResources false // Caused inconsistencies between local and CI builds
            vcsInfo.include false // For reproducible builds independent from git history
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    defaultConfig {
        namespace 'org.rivchain.cuplink'
        applicationId "org.rivchain.cuplink"
        minSdkVersion 21
        compileSdkVersion 34
        targetSdkVersion 34
        versionCode 740
        versionName "0.6.14.5"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        resourceConfigurations += ['de', 'en', 'es', 'fr', 'nb-rNO', 'pt-rBR', 'ru', 'uk', 'zh']
        setProperty("archivesBaseName", project.getParent().name + "-" + versionName)
    }

    kotlinOptions {
        jvmTarget = '17'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
    lint {
        abortOnError false
    }
}

def propertiesFile = rootProject.file("keystore.properties")
if (propertiesFile.canRead()) {
    Properties props = new Properties()
    props.load(new FileInputStream(propertiesFile))

    if (props!=null
            && props.containsKey('KEYSTORE_FILE_PATH')
            && props.containsKey('KEYSTORE_PASSWORD')
            && props.containsKey('KEY_ALIAS')
            && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['KEYSTORE_FILE_PATH'])
        android.signingConfigs.release.storePassword = props['KEYSTORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'keystore.properties not found'
    android.buildTypes.release.signingConfig = null
}

dependencies {
    implementation 'ch.acra:acra-core:5.11.3'
    implementation 'ch.acra:acra-http:5.11.3'
    implementation 'ch.acra:acra-dialog:5.11.3'

    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation('com.journeyapps:zxing-android-embedded:4.3.0') { transitive = false }
    implementation 'com.google.zxing:core:3.5.3'
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'com.hbb20:ccp:2.7.3'
    implementation 'com.vincentbrison.openlibraries.android:dualcache:3.1.1'
    implementation 'com.vincentbrison.openlibraries.android:dualcache-jsonserializer:3.1.1'
    implementation 'com.github.joshjdevl.libsodiumjni:libsodium-jni-aar:2.0.2'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'androidx.viewpager2:viewpager2:1.1.0'
    implementation 'androidx.car.app:app:1.4.0'
    implementation 'net.mm2d.mmupnp:mmupnp:3.1.6'
    implementation 'org.tdf:rlp:1.1.20'
    implementation 'com.github.nikhiljainlive.blockies_android:blockiesview:v0.2.1'
    implementation 'com.github.nikhiljainlive.blockies_android:blockiesgenerator:v0.2.1'
    implementation 'com.github.nikhiljainlive.blockies_android:blockiesglide:v0.2.1'
    implementation 'org.rivchain:libwebrtc:1.0.0@aar'
    implementation 'org.rivchain:mesh:0.4.7.18@aar'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'androidx.security:security-crypto:1.1.0-alpha06'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.7'
    implementation "androidx.lifecycle:lifecycle-process:2.8.7"
    implementation 'com.github.wendykierp:JTransforms:3.1'
}
